apply plugin: 'java'
apply plugin: 'maven'

group = 'com.codeborne'
archivesBaseName = 'mobileid'
version = '1.1'

defaultTasks 'clean', 'test', 'install'

repositories{
  mavenCentral()
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
  }
  test {
    java {
      srcDir 'test'
    }
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
[compileJava, compileTestJava, javadoc]*.options.collect {options ->
  options.encoding = 'UTF-8'
}

[compileJava, compileTestJava]*.options.collect {options ->
  options.debug = true
  options.deprecation = true
  options.compilerArgs.add '-Xlint'
  options.compilerArgs.add '-Xlint:-unchecked'
  options.compilerArgs.add '-Xlint:-serial'
  options.compilerArgs.add '-Xlint:-rawtypes'
}

dependencies {
  compile 'org.apache.axis:axis:1.4'
  compile 'org.apache.axis:axis-jaxrpc:1.4'
  compile 'commons-discovery:commons-discovery:0.5'
  compile 'wsdl4j:wsdl4j:1.6.3'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-core:1.3'
  testCompile 'org.mockito:mockito-all:1.10.19'
}

task libs(type: Sync) {
  from configurations.compile
  from configurations.testCompile
  into "$buildDir/lib"
}

compileJava.dependsOn libs

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.7'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}

